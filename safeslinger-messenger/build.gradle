apply plugin: 'com.android.application'

android {
    compileSdkVersion 21
    buildToolsVersion "21.1.2"

    defaultConfig {
        applicationId "edu.cmu.cylab.starslinger"
        minSdkVersion 9
        targetSdkVersion 21
    }
    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            res.srcDirs = ['res']
            assets.srcDirs = ['assets']
        }
    }
    lintOptions {
        abortOnError false
    }

    /*
     * For store release, create/add ~/.gradle/gradle.properties with this content:
     * releaseMsgrKeyStoreLocation=/home/release.key.store
     * releaseMsgrKeyStorePassword=xxxxxxxx
     * releaseMsgrKeyAlias=key.alias
     * releaseMsgrKeyPassword=xxxxxxxx
     */
    if (project.hasProperty('releaseMsgrKeyStoreLocation') &&
            project.hasProperty('releaseMsgrKeyStorePassword') &&
            project.hasProperty('releaseMsgrKeyAlias') &&
            project.hasProperty('releaseMsgrKeyPassword')) {
        println "Signing Messenger RELEASE build…"

        signingConfigs {
            release {
                storeFile file(releaseMsgrKeyStoreLocation)
                storePassword releaseMsgrKeyStorePassword
                keyAlias releaseMsgrKeyAlias
                keyPassword releaseMsgrKeyPassword
            }
        }

        buildTypes.release.signingConfig = signingConfigs.release
    } else {
        buildTypes.release.signingConfig = null
    }

    /*
     * For custom debug, create/add ~/.gradle/gradle.properties with this content:
     * debugKeyStoreLocation=/home/debug.key.store
     */
    if (project.hasProperty('debugKeyStoreLocation')) {
        println "Signing general DEBUG build…"

        signingConfigs {
            debug {
                storeFile file(debugKeyStoreLocation)
            }
        }

        buildTypes.debug.signingConfig = signingConfigs.debug
    } else {
        buildTypes.debug.signingConfig = null
    }
}

dependencies {
    compile project(':android-vcard')
    compile project(':safeslinger-exchange')
    compile 'com.android.support:appcompat-v7:19.1.0'
    compile 'com.android.support:support-v4:21.0.3'
    compile 'com.google.android.gms:play-services:6.5.87'
}
