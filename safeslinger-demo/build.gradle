apply plugin: 'com.android.application'

android {
    compileSdkVersion 21
    buildToolsVersion "21.1.2"

    defaultConfig {
        applicationId "edu.cmu.cylab.starslinger.demo"
        minSdkVersion 8
        targetSdkVersion 21
    }
    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            res.srcDirs = ['res']
        }
    }
    lintOptions {
        abortOnError false
    }

    /*
     * For store release, create/add ~/.gradle/gradle.properties with this content:
     * releaseDemoKeyStoreLocation=/home/release.key.store
     * releaseDemoKeyStorePassword=xxxxxxxx
     * releaseDemoKeyAlias=key.alias
     * releaseDemoKeyPassword=xxxxxxxx
     */
    if (project.hasProperty('releaseDemoKeyStoreLocation') &&
            project.hasProperty('releaseDemoKeyStorePassword') &&
            project.hasProperty('releaseDemoKeyAlias') &&
            project.hasProperty('releaseDemoKeyPassword')) {
        println "Signing Demo RELEASE build…"

        signingConfigs {
            release {
                storeFile file(releaseDemoKeyStoreLocation)
                storePassword releaseDemoKeyStorePassword
                keyAlias releaseDemoKeyAlias
                keyPassword releaseDemoKeyPassword
            }
        }

        buildTypes.release.signingConfig = signingConfigs.release
    } else {
        buildTypes.release.signingConfig = null
    }

    /*
     * For custom debug, create/add ~/.gradle/gradle.properties with this content:
     * debugKeyStoreLocation=/home/debug.key.store
     */
    if (project.hasProperty('debugKeyStoreLocation')) {
        println "Signing general DEBUG build…"

        signingConfigs {
            debug {
                storeFile file(debugKeyStoreLocation)
            }
        }

        buildTypes.debug.signingConfig = signingConfigs.debug
    } else {
        buildTypes.debug.signingConfig = null
    }
}

dependencies {
    compile project(':safeslinger-exchange')
    compile 'com.android.support:appcompat-v7:19.1.0'
}
